# GasMix Controller

alias paGas1 d0
alias paGas2 d1
alias paOutput d2
alias vpGas1 d3
alias vpGas2 d4
alias switch d5

define R 8.314462

# Max VOut [m3] - Use max 0.01 for VP and 0.1 for TVP
define Vout 0.01
# Step of decreasing Vout based on max Vin
define VStep 0.0005

define MinInputPressure 3000
define MaxOutputPressure 35000 # 35MPa

alias CurrentVout r8
alias Gas1Ratio r9
alias Tout r10
alias Pout r11
alias nout r12
alias n1 r13
alias n2 r14
alias V1 r15
alias V2 r16

div Gas1Ratio 2 3

Reset:
s vpGas1 Setting 0
s vpGas1 On 1
s vpGas1 Lock 0
s vpGas2 Setting 0
s vpGas2 On 1
s vpGas2 Lock 0

Loop:
l r0 switch Setting
beq r0 0 Reset
l r0 paGas1 Pressure
l r1 paGas2 Pressure
sgt r0 r0 MinInputPressure
sgt r1 r1 MinInputPressure
and r2 r0 r1
beq r2 0 Reset

l r3 paOutput Pressure
bgt r3 MaxOutputPressure Reset

move CurrentVout Vout
RecalcWithLowerVout:
# Calculate output Pressure
l r0 paGas1 Pressure
l r1 paGas2 Pressure
mul r0 r0 1000
mul r1 r1 1000
move r5 Gas1Ratio
mul r3 r0 r5
sub r4 1 r5
mul r4 r4 r1
add Pout r3 r4

# Calculate output Temperature
l r0 paGas1 Temperature
l r1 paGas2 Temperature
move r5 Gas1Ratio
mul r3 r0 r5
sub r4 1 r5
mul r4 r4 r1
add Tout r3 r4

# Calculate output moles
mul r3 Pout CurrentVout
mul r4 R Tout
div nout r3 r4

# Calculate moles for Gas1 and Gas2
move r5 Gas1Ratio
mul n1 nout r5
sub r4 1 r5
mul n2 nout r4

# Calculate Volume for Gas1
l r0 paGas1 Temperature
l r1 paGas1 Pressure
mul r1 r1 1000
mul r3 n1 R
mul r3 r3 r0
div V1 r3 r1
mul V1 V1 1000
brle V1 10 5
sub CurrentVout CurrentVout VStep
ble CurrentVout 0 Reset
j RecalcWithLowerVout

# Calculate Volume for Gas2
l r0 paGas2 Temperature
l r1 paGas2 Pressure
mul r1 r1 1000
mul r3 n2 R
mul r3 r3 r0
div V2 r3 r1
mul V2 V2 1000
brle V2 10 5
sub CurrentVout CurrentVout VStep
ble CurrentVout 0 Reset
j RecalcWithLowerVout

# Set Volumes for Gases
s vpGas1 Setting V1
s vpGas2 Setting V2

j Loop